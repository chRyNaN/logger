import org.gradle.api.publish.maven.internal.artifact.FileBasedMavenArtifact
import com.chrynan.logger.buildSrc.LibraryConstants

group LibraryConstants.group
version LibraryConstants.versionName

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.61"
        classpath 'digital.wup:android-maven-publish:3.6.2'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        google()
        jcenter()
    }
}

subprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    bintray {
        user = project.findProperty("BINTRAY_USER") ?: System.getenv('BINTRAY_USER')
        key = project.findProperty("BINTRAY_KEY") ?: System.getenv('BINTRAY_KEY')

        pkg {
            repo = LibraryConstants.bintrayRepo
            name = LibraryConstants.repoName
            licenses = [LibraryConstants.license]
            vcsUrl = LibraryConstants.vcsUrl

            version {
                name = LibraryConstants.versionName
                desc = LibraryConstants.versionDescription
                released = new Date()
                vcsTag = LibraryConstants.versionName
            }
        }
    }

    bintrayUpload.doFirst {
        publications = publishing.publications.collect {
            File moduleFile = project.buildDir.toPath()
                    .resolve("publications/${it.name}/module.json").toFile()

            if (moduleFile.exists()) {
                it.artifact(new FileBasedMavenArtifact(moduleFile) {
                    @Override
                    protected String getDefaultExtension() {
                        return "module"
                    }
                })
            }

            it.name
        }
    }

    bintrayUpload.dependsOn publishToMavenLocal
}